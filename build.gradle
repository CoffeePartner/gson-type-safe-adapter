// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://dl.bintray.com/dieyi/maven"
        }
    }
    dependencies {

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


group = "com.dieyidezui.gson"
//name = "type-safe-adapter"
version = "1.0.0"


apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.2'
    testCompile 'junit:junit:4.12'
}


apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
afterEvaluate {
    task sourcesJar(type: Jar) {
        from sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
    tasks.install {
        repositories.mavenInstaller {
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    packaging 'jar'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'dieyidezui'
                            name '耿万鹏'
                            email 'dieyidezui@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "type-safe-adapter"
        websiteUrl = "https://github.com/CoffeePartner/gson-type-safe-adapter"
        vcsUrl = "git@github.com:CoffeePartner/gson-type-safe-adapter.git"
        licenses = ["Apache-2.0"]
        publish = true
    }
}

